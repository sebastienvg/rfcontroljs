module.exports = (helper) ->
  # mapping for decoding
  pulsesToBinaryMapping = {
    '01': '1' #binary 1
    '10': '0' #binary 0
    '12': ''    #footer
  }
  # same for send
  binaryToPulse = {
    '1': '01'
    '0': '10'
  }
  return protocolInfo = {
    name: 'switch10'
    type: 'switch'
    values:
      id:
        type: "binary"
      state:
        type: "boolean"
      unit:
        type: "number"
    brands: ["Nexxtech Wireless Remotely Operated Switch no 28064"]
    pulseLengths: [300, 600, 23000]
    pulseCount: 38
    decodePulses: (pulses) ->
      # pulses is something like: '01100110011010101001101010010101100112'
      # we first map the sequences to binary
      binary = helper.map(pulses, pulsesToBinaryMapping)
      # binary is now something like: '101010000100011101'
      # now we extract the data from that string
      # | 10101000|01000111| 0     |1
      # | ID?     |unit?   | State |?
      return result = {
        id: helper.binaryToNumber(binary, 0, 7)
        unit: helper.binaryToNumber(binary, 8, 15)
        state: helper.binaryToBoolean(binary, 16)
      }
    encodeMessage: (message) ->
      id = helper.map(helper.numberToBinary(message.id, 8), binaryToPulse)
      state = (if message.state then binaryToPulse['1'] else binaryToPulse['0'])
      unit = helper.map(helper.numberToBinary(message.unit, 8), binaryToPulse)
      return "#{id}#{unit}#{state}1012"
  }
